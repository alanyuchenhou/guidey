apply plugin: 'com.android.application'

android {
    buildToolsVersion rootProject.androidBuildToolsVersion
    compileSdkVersion rootProject.androidCompileSdkVersion

    defaultConfig {

        targetSdkVersion androidTargetSdkVersion
        minSdkVersion androidMinSdkVersion
        versionCode buildVersionCode
        versionName buildVersionName
        multiDexEnabled true
        dimension "locale"
        buildConfigField("String[]", "BUILT_IN_LOCALE", '{"en-US","es-ES","fr-FR","de-DE"}');
        applicationId 'com.tomtom.mapssdk.examplesapp'

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def file = output.outputFile
                output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + versionName + ".apk"))
            }
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        merge 'META-INF/services/javax.annotation.processing.Processor'
        merge 'META-INF/DEPENDENCIES.txt'
        merge 'META-INF/LICENSE.txt'
        merge 'META-INF/NOTICE.txt'
        merge 'META-INF/NOTICE'
        merge 'META-INF/LICENSE'
        merge 'META-INF/DEPENDENCIES'
        merge 'META-INF/notice.txt'
        merge 'META-INF/license.txt'
        merge 'META-INF/dependencies.txt'
        merge 'META-INF/LGPL2.1'
        merge 'constants.properties'
        merge 'META-INF/licence.txt'
        merge 'META-INF/rxjava.properties'
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            // All the usual Gradle options.
            jvmArgs '-XX:MaxPermSize=256m'
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
            }
        }
    }

    configurations.all {
        resolutionStrategy.force "com.android.support:support-annotations:$rootProject.androidVersions.support"
        resolutionStrategy.force "com.android.support:support-v4:$rootProject.androidVersions.support"
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:2.0.1'
    }
}

dependencies {
        //doc_dependencies_sample_app_start
        //dependency for display map and core code
        compile("com.tomtom.online:sdk-maps:$rootProject.internalVersions.sdk@aar") {
            transitive = true
        }
        //dependency for search
        compile("com.tomtom.online:sdk-search:$rootProject.internalVersions.sdk@aar") {
            transitive = true
        }
        //dependency for route planner
        compile("com.tomtom.online:sdk-routing:$rootProject.internalVersions.sdk@aar") {
            transitive = true
        }
        //dependency for rx extensions
        compile("com.tomtom.online:sdk-maps-rx-extensions:$rootProject.internalVersions.sdk@aar") {
            transitive = true
        }

        //dependency for maps ui extensions
        compile("com.tomtom.online:sdk-maps-ui-extensions:$rootProject.internalVersions.sdk@aar") {
            transitive = true
        }

        //doc_dependencies_sample_app_stop

    compile "com.github.florent37:singledateandtimepicker:$rootProject.libVersions.singledateandtimepicker"

}
